#+ summary: Get all taxon-taxon relation nanopublications
#+ method: GET
#+ pagination: 1000
#+ endpoint_in_url: False

prefix np: <http://www.nanopub.org/nschema#>
prefix npa: <http://purl.org/nanopub/admin/>
prefix npx: <http://purl.org/nanopub/x/>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix dct: <http://purl.org/dc/terms/>
prefix biolink: <https://w3id.org/biolink/vocab/>
prefix nt: <https://w3id.org/np/o/ntemplate/>
prefix kbiodiv: <https://w3id.org/kpxl/biodiv/terms/>

select distinct ?np ?label ?subjtaxon ?subjtaxonLabel ?subjlcstage ?pred ?predLabel ?objtaxon ?objtaxonLabel ?objlcstage ?date where {
  graph npa:graph {
    ?np npa:hasHeadGraph ?h .
    ?np dct:created ?date .
    ?np npa:hasValidSignatureForPublicKey ?pubkey .
  }
  graph ?h {
    ?np np:hasAssertion ?a .
    ?np np:hasPublicationInfo ?i.
  }
  graph ?a {
    ?association a biolink:OrganismTaxonToOrganismTaxonAssociation .
    ?association biolink:subject ?subjtaxon .
    optional { ?association kbiodiv:hasSubjectLifeCycleStage ?subjlcstage . }
    ?association biolink:predicate ?pred .
    ?association biolink:object ?objtaxon .
    optional { ?association kbiodiv:hasObjectLifeCycleStage ?objlcstage . }
  }
  graph ?i {
    ?np dct:creator ?__creator_iri .
    optional { ?subjtaxon nt:hasLabelFromApi ?subjtaxonLabel . }
    optional { ?pred nt:hasLabelFromApi ?predLabel . }
    optional { ?objtaxon nt:hasLabelFromApi ?objtaxonLabel . }
  }
  bind(coalesce(replace(str(?subjtaxonLabel), " - .*", ""), replace(str(?subjtaxon), "^.*/([^/]*)$", "$1")) as ?subjx)
  bind(replace(str(?predLabel), " - .*", "") as ?predx)
  bind(coalesce(replace(str(?objtaxonLabel), " - .*", ""), replace(str(?objtaxon), "^.*/([^/]*)$", "$1")) as ?objx)
  bind(concat(?subjx, " ", ?predx, " ", ?objx) as ?label)
  filter not exists {
    graph npa:graph {
      ?newversion npa:hasHeadGraph ?nh .
      ?newversion npa:hasValidSignatureForPublicKey ?pubkey .
    }
    graph ?nh {
      ?newversion np:hasPublicationInfo ?ni .
    }
    graph ?ni {
      ?newversion npx:supersedes ?np .
    }
  }
  filter not exists {
    graph npa:graph {
      ?retraction npa:hasHeadGraph ?rh .
      ?retraction npa:hasValidSignatureForPublicKey ?pubkey .
    }
    graph ?rh {
      ?retraction np:hasAssertion ?ra .
    }
    graph ?ra {
      ?somebody npx:retracts ?np .
    }
  }
}
order by desc(?date)
