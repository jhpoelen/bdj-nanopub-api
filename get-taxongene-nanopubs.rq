#+ summary: Get all taxon-gene relation nanopublications
#+ method: GET
#+ pagination: 1000
#+ endpoint_in_url: False

prefix np: <http://www.nanopub.org/nschema#>
prefix npa: <http://purl.org/nanopub/admin/>
prefix npx: <http://purl.org/nanopub/x/>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix dct: <http://purl.org/dc/terms/>
prefix biolink: <https://w3id.org/biolink/vocab/>
prefix nt: <https://w3id.org/np/o/ntemplate/>
prefix kbiodiv: <https://w3id.org/kpxl/biodiv/terms/>

select distinct ?np ?label ?taxon ?taxonLabel ?lcstage ?pred ?predLabel ?gene ?geneLabel ?date where {
  graph npa:graph {
    ?np npa:hasHeadGraph ?h .
    ?np dct:created ?date .
    ?np npa:hasValidSignatureForPublicKey ?pubkey .
  }
  graph ?h {
    ?np np:hasAssertion ?a .
    ?np np:hasPublicationInfo ?i.
  }
  graph ?a {
    ?association a <https://w3id.org/kpxl/biodiv/terms/OrganismTaxonToGeneAssociation> .
    ?association biolink:subject ?taxon .
    optional { ?association kbiodiv:hasSubjectLifeCycleStage ?lcstage . }
    ?association biolink:predicate ?pred .
    ?association biolink:object ?gene .
  }
  graph ?i {
    ?np dct:creator ?__creator_iri .
    optional { ?taxon nt:hasLabelFromApi ?taxonLabel . }
    optional { ?pred nt:hasLabelFromApi ?predLabel . }
    optional { ?gene nt:hasLabelFromApi ?geneLabel . }
  }
  bind(coalesce(replace(str(?taxonLabel), " - .*", ""), replace(str(?taxon), "^.*/([^/]*)$", "$1")) as ?taxonx)
  bind(replace(str(?predLabel), " - .*", "") as ?predx)
  bind(coalesce(replace(str(?geneLabel), " - .*", ""), replace(str(?gene), "^.*/([^/]*)$", "$1")) as ?genex)
  bind(concat(?taxonx, " ", ?predx, " ", ?genex) as ?label)
  filter not exists {
    graph npa:graph {
      ?newversion npa:hasHeadGraph ?nh .
      ?newversion npa:hasValidSignatureForPublicKey ?pubkey .
    }
    graph ?nh {
      ?newversion np:hasPublicationInfo ?ni .
    }
    graph ?ni {
      ?newversion npx:supersedes ?np .
    }
  }
  filter not exists {
    graph npa:graph {
      ?retraction npa:hasHeadGraph ?rh .
      ?retraction npa:hasValidSignatureForPublicKey ?pubkey .
    }
    graph ?rh {
      ?retraction np:hasAssertion ?ra .
    }
    graph ?ra {
      ?somebody npx:retracts ?np .
    }
  }
}
order by desc(?date)
